<!DOCTYPE html>
<!-- Copyright (C) 2011-2012 Frogtoss Games, Inc.  See glmike.js for license. -->
<HTML>
  <HEAD>
    <TITLE>glMikeFirst</TITLE>
    <META http-equiv="content-type" content="text-html; charset=ISO-8859-1">

    <SCRIPT type="text/javascript" src="lib/gl-matrix.js"></SCRIPT>
    <SCRIPT type="text/javascript" src="lib/webgl-utils.js"></SCRIPT>
    <SCRIPT type="text/javascript" src="lib/ftgplane.js"></SCRIPT>
    <SCRIPt type="text/javascript" src="lib/webgl-debug.js"></SCRIPT>
    <SCRIPT src="https://ajax.googleapis.com/ajax/libs/jquery/1.7.1/jquery.min.js"></SCRIPT>
    <SCRIPT src="glmike.js" type="text/javascript"></SCRIPT>

    <!-- Post shading -->
    <SCRIPT id="vpost" type="x-shader/x-vertex">
                   // Common vertex post process shader -- perfect for drawTextureToViewport
                   attribute vec3 aVertexPosition;
                   attribute vec2 aTextureCoord;

                   uniform vec2 uTextureSize;

                   uniform mat4 uMVMatrix;
                   uniform mat4 uPMatrix;

                   varying vec2 vTextureCoord;
                   
                   void main( void ) {
                       gl_Position = uPMatrix * uMVMatrix * vec4( aVertexPosition, 1 );
                       vTextureCoord = aTextureCoord;
                   }
    </SCRIPT>

    <SCRIPT id="fpost" type="x-shader/x-fragment">
                   precision mediump float;
                   varying vec2 vTextureCoord;
                   uniform sampler2D uSampler;

                   void main( void ) {
                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );
                       
                       gl_FragColor = col;
                   }
    </SCRIPT>


    <SCRIPT id="fpostGrayscale" type="x-shader/x-fragment">
                   precision mediump float;
                   varying vec2 vTextureCoord;
                   uniform sampler2D uSampler;
                   uniform float grayness; /* 0 to 1 */

                   void main( void ) {
                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );

                       vec3 gray = mix( col.rgb, vec3( dot( col.rgb, vec3( 0.3, 0.59, 0.11 ) ) ), grayness );
                       //vec3 gray = mix( col.rgb, vec3( dot( col.rgb, vec3( 0.3, 0.59, 0.11 ) ) ), 0.8 );
                       
                       gl_FragColor = vec4( gray, col.a );
                   }
    </SCRIPT>


    <SCRIPT id="fpostblur" type="x-shader/x-fragment">
                   precision mediump float;
                   varying vec2 vTextureCoord;
                   uniform sampler2D uSampler;
                   uniform vec2 uTextureSize;

                   uniform float radius;


                   void main( void ) {
                       float oh = radius / uTextureSize[0];
                       float ov = radius / uTextureSize[1];

                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );
                       vec4 col2 = texture2D( uSampler, vec2( vTextureCoord.s+oh, vTextureCoord.t+ov ) );

                       vec4 sum = vec4(0,0,0,0);
                       sum += texture2D( uSampler, vec2( vTextureCoord.s-oh, vTextureCoord.t-ov ) )*0.0625;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s   , vTextureCoord.t-ov ) )*0.125;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s+oh, vTextureCoord.t-ov ) )*0.0625;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s-oh, vTextureCoord.t    ) )*0.125;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s   , vTextureCoord.t    ) )*0.25;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s+oh, vTextureCoord.t    ) )*0.125;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s-oh, vTextureCoord.t+ov ) )*0.0625;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s   , vTextureCoord.t+ov ) )*0.125;
                       sum += texture2D( uSampler, vec2( vTextureCoord.s+oh, vTextureCoord.t+ov ) )*0.0625;


                       //gl_FragColor = texture2D( uSampler, vec2( 0.1,0.1 ) );
                       gl_FragColor = sum;
                       //gl_FragColor = vec4(col.rgb, col2.a);
                   }
               </SCRIPT>

               <!-- This shader based on http://www.gamerendering.com/2009/01/14/ssao/ -->
    <SCRIPT id="fpostssao" type="x-shader/x-fragment">

                    precision mediump float;
                    varying vec2 vTextureCoord;
                    uniform vec2 uTextureSize;

                    uniform sampler2D uRandomNormals;
                    uniform sampler2D uNormalMap;

                    uniform float uTotalShadowStrength;
                    uniform float uShadowStrength;
                    uniform float uRandomOffset;
                    uniform float uFalloff;
                    uniform float uRadius;
                    #define SAMPLES 16 // 10 is good
                    const float invSamples = 1.0/16.0;

                    void main(void)
                    {
                        // these are the random vectors inside a unit sphere
                        vec3 pSphere[16];
                        pSphere[0] = vec3(0.53812504, 0.18565957, -0.43192);
                        pSphere[1] = vec3(0.13790712, 0.24864247, 0.44301823);
                        pSphere[2] = vec3(0.33715037, 0.56794053, -0.005789503);
                        pSphere[3] = vec3(-0.6999805, -0.04511441, -0.0019965635);
                        pSphere[4] = vec3(0.06896307, -0.15983082, -0.85477847);
                        pSphere[5] = vec3(0.056099437, 0.006954967, -0.1843352);
                        pSphere[6] = vec3(-0.014653638, 0.14027752, 0.0762037);
                        pSphere[7] = vec3(0.010019933, -0.1924225, -0.034443386);
                        pSphere[8] = vec3(-0.35775623, -0.5301969, -0.43581226);
                        pSphere[9] = vec3(-0.3169221, 0.106360726, 0.015860917);
                        pSphere[10] = vec3(0.010350345, -0.58698344, 0.0046293875);
                        pSphere[11] = vec3(-0.08972908, -0.49408212, 0.3287904);
                        pSphere[12] = vec3(0.7119986, -0.0154690035, -0.09183723);
                        pSphere[13] = vec3(-0.053382345, 0.059675813, -0.5411899);
                        pSphere[14] = vec3(0.035267662, -0.063188605, 0.54602677);
                        pSphere[15] = vec3(-0.47761092, 0.2847911, -0.0271716);
                        //vec3 pSphere[16] = vec3[](vec3(0.53812504, 0.18565957, -0.43192),vec3(0.13790712, 0.24864247, 0.44301823),vec3(0.33715037, 0.56794053, -0.005789503),vec3(-0.6999805, -0.04511441, -0.0019965635),vec3(0.06896307, -0.15983082, -0.85477847),vec3(0.056099437, 0.006954967, -0.1843352),vec3(-0.014653638, 0.14027752, 0.0762037),vec3(0.010019933, -0.1924225, -0.034443386),vec3(-0.35775623, -0.5301969, -0.43581226),vec3(-0.3169221, 0.106360726, 0.015860917),vec3(0.010350345, -0.58698344, 0.0046293875),vec3(-0.08972908, -0.49408212, 0.3287904),vec3(0.7119986, -0.0154690035, -0.09183723),vec3(-0.053382345, 0.059675813, -0.5411899),vec3(0.035267662, -0.063188605, 0.54602677),vec3(-0.47761092, 0.2847911, -0.0271716));
                        //const vec3 pSphere[8] = vec3[](vec3(0.24710192, 0.6445882, 0.033550154),vec3(0.00991752, -0.21947019, 0.7196721),vec3(0.25109035, -0.1787317, -0.011580509),vec3(-0.08781511, 0.44514698, 0.56647956),vec3(-0.011737816, -0.0643377, 0.16030222),vec3(0.035941467, 0.04990871, -0.46533614),vec3(-0.058801126, 0.7347013, -0.25399926),vec3(-0.24799341, -0.022052078, -0.13399573));
                        //const vec3 pSphere[12] = vec3[](vec3(-0.13657719, 0.30651027, 0.16118456),vec3(-0.14714938, 0.33245975, -0.113095455),vec3(0.030659059, 0.27887347, -0.7332209),vec3(0.009913514, -0.89884496, 0.07381549),vec3(0.040318526, 0.40091, 0.6847858),vec3(0.22311053, -0.3039437, -0.19340435),vec3(0.36235332, 0.21894878, -0.05407306),vec3(-0.15198798, -0.38409665, -0.46785462),vec3(-0.013492276, -0.5345803, 0.11307949),vec3(-0.4972847, 0.037064247, -0.4381323),vec3(-0.024175806, -0.008928787, 0.17719103),vec3(0.694014, -0.122672155, 0.33098832));
                        //const vec3 pSphere[10] = vec3[](vec3(-0.010735935, 0.01647018, 0.0062425877),vec3(-0.06533369, 0.3647007, -0.13746321),vec3(-0.6539235, -0.016726388, -0.53000957),vec3(0.40958285, 0.0052428036, -0.5591124),vec3(-0.1465366, 0.09899267, 0.15571679),vec3(-0.44122112, -0.5458797, 0.04912532),vec3(0.03755566, -0.10961345, -0.33040273),vec3(0.019100213, 0.29652783, 0.066237666),vec3(0.8765323, 0.011236004, 0.28265962),vec3(0.29264435, -0.40794238, 0.15964167));

                        // grab a normal for reflecting the sample rays later on
                        //vec2 texelCoord = vTextureCoord*vec2(20,20);//*uTextureSize;
                        vec4 fresPixel = texture2D(uRandomNormals,vTextureCoord*uRandomOffset);
                        vec3 fres = normalize((fresPixel.xyz*2.0) - vec3(1.0));

                        vec4 currentPixelSample = texture2D(uNormalMap,vTextureCoord);

                        float currentPixelDepth = currentPixelSample.a;

                        // current fragment coords in screen space
                        vec3 ep = vec3(vTextureCoord.xy,currentPixelDepth);
                        // get the normal of current fragment
                        vec3 norm = currentPixelSample.xyz;

                        float bl = 0.0;
                        // adjust for the depth ( not shure if this is good..)
                        float radD = uRadius;// /currentPixelDepth;

                        vec3 ray, se, occNorm;
                        float occluderDepth, depthDifference, normDiff;

                        vec3 ratioSkew = vec3(1, 1, 1);
                        //vec3 ratioSkew = vec3(uTextureSize, 1);

                        for(int i=0; i < SAMPLES; ++i)
                        {
                            // get a vector (randomized inside of a sphere with radius 1.0) from a texture and reflect it
                            ray = radD*reflect(pSphere[i],fres)*ratioSkew;

                            // if the ray is outside the hemisphere then change direction
                            se = ep + sign(dot(ray,norm) )*ray;

                            // get the depth of the occluder fragment
                            vec4 occluderFragment = texture2D(uNormalMap,se.xy);

                            // get the normal of the occluder fragment
                            occNorm = occluderFragment.xyz;

                            // if depthDifference is negative = occluder is behind current fragment
                            depthDifference = currentPixelDepth-occluderFragment.a;

                            // calculate the difference between the normals as a weight

                            normDiff = 1.0; // (1.0-dot(occNorm,norm));
                            // the uFalloff equation, starts at uFalloff and is kind of 1/x^2 falling
                            bl += step(uFalloff,depthDifference)*normDiff*(1.0-smoothstep(uFalloff,uShadowStrength,depthDifference));
                        }

                        // output the result
                        float ao = 1.0-uTotalShadowStrength*bl*invSamples;

                        // this will be layered on the colour render
                        //fresPixel.a = 1.0;
                        //gl_FragColor = fresPixel;//vec4(fres*0.5+0.5,1.0);
                        //gl_FragColor = vec4(currentPixelSample.xyz, 1);
                        //gl_FragColor = vec4(currentPixelSample.a, currentPixelSample.a, currentPixelSample.a, 1);
                        //gl_FragColor = vec4(ao,ao,ao,1);
                        gl_FragColor = vec4(0,0,0,1.0-ao);
                     
                    }


    </SCRIPT>

    <!-- Forward shading -->
    <SCRIPT id="vdef" type="x-shader/x-vertex">
                   // A copy of the def vshader from the js source.
                   attribute vec3 aVertexPosition;
                   attribute vec2 aTextureCoord;

                   uniform mat4 uMVMatrix;
                   uniform mat4 uPMatrix;
                   uniform mat4 uBoxMatrix;

                   varying vec2 vTextureCoord;

                   void main( void ) {
                       gl_Position = uPMatrix * uMVMatrix * uBoxMatrix * vec4(aVertexPosition, 1 );
                       vTextureCoord = aTextureCoord;
                   }
    </SCRIPT>

    <SCRIPT id="fshadowstamp" type="x-shader/x-fragment">
                   precision mediump float;
                   varying vec2 vTextureCoord;
                   uniform sampler2D uSampler;

                   void main( void ) {
                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );

                       gl_FragColor = vec4( 0.0, 0.0, 0.0, col.a );
                   }
    </SCRIPT>


    <SCRIPT id="vNormalAndDepth" type="x-shader/x-vertex">
                   attribute vec3 aVertexPosition;
                   attribute vec3 aVertexNormal;
                   attribute vec3 aVertexTangent;
                   attribute vec2 aTextureCoord;

                   uniform mat4 uMVMatrix;
                   uniform mat4 uPMatrix;
                   uniform mat3 uNMatrix;

                   uniform mat4 uBoxMatrix;

                   varying vec2 vTextureCoord;
                   varying mat3 vTangentFrame;

                   varying vec4 vPosition;

                   void main( void ) {
                       vec4 pos = uPMatrix * uMVMatrix * uBoxMatrix * vec4( aVertexPosition, 1 );
                       gl_Position = pos;
                       vTextureCoord = aTextureCoord;

                       vec3 n = normalize( aVertexNormal );
                       vec3 t = normalize( aVertexTangent );
                       vec3 b = cross(n, t);

                       vTangentFrame[0] = uNMatrix * b;
                       vTangentFrame[1] = uNMatrix * t;
                       vTangentFrame[2] = uNMatrix * n;

                       vPosition = pos;
                   }
    </SCRIPT>


    <SCRIPT id="fNormalAndDepth" type="x-shader/x-fragment">
                   precision mediump float;

                   varying vec2 vTextureCoord;

                   uniform sampler2D uSampler;
                   uniform sampler2D uNormalSampler;

                   uniform mat4 uMVMatrix;
                   uniform mat4 uPMatrix;
                   uniform mat3 uNMatrix;

                   uniform float uZOffset;

                   //uniform float uNear;
                   //uniform flaot uFar;
                   const float uNear = 900.0;
                   const float uFar = 925.0;
                   
                   varying mat3 vTangentFrame;
                   varying vec4 vPosition;
                   
                   void main( void ) {
                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );
                       // This is to make up for the fact that we must write depth to the alpha channel
                       // Combine with gl.disable(gl.BLEND);
                       if ( col.a < 0.5 ) 
                        discard;

                       vec4 nCol = texture2D( uNormalSampler, vTextureCoord );
                       vec3 map = normalize(nCol.xyz * 2.0 - 1.0);

                       map = normalize( vTangentFrame * map );

                       float vertDepth = vPosition.z;
                       vertDepth += uZOffset * 5.0;// - nCol.a * 5.0 ;

                       float depth = (vertDepth-uNear)/(uFar-uNear);

                       //gl_FragColor = vec4(1, 1, 1, depth);
                       //gl_FragColor = vec4(depth, depth, depth, 1);
                       gl_FragColor = vec4(0.5*map+0.5, depth);
                       //gl_FragColor = vec4(0.5*map+0.5, col.a);
                       //gl_FragDepth = depth; // This output doesn't exist en GLES2.0!!
                   }

    </SCRIPT>


    <SCRIPT id="vbump" type="x-shader/x-vertex">
                   attribute vec3 aVertexPosition;
                   attribute vec3 aVertexNormal;
                   attribute vec3 aVertexTangent;
                   attribute vec2 aTextureCoord;

                   uniform mat4 uMVMatrix;
                   uniform mat4 uPMatrix;
                   uniform mat3 uNMatrix;

                   uniform mat4 uBoxMatrix;

                   uniform vec3 uLightDirection;
                   uniform vec3 uLightColor;

                   varying vec2 vTextureCoord;
                   varying vec3 vLightDir;
                   varying vec3 vEyeDir;

                   void main( void ) {
                       gl_Position = uPMatrix * uMVMatrix * uBoxMatrix * vec4(aVertexPosition, 1 );
                       vTextureCoord = aTextureCoord;

                       vec3 n = normalize(aVertexNormal);
                       vec3 t = normalize(aVertexTangent);
                       vec3 b = cross(n, t);

                       vec3 p = uNMatrix * normalize(uLightDirection);

                       vLightDir.x = dot(p, t);
                       vLightDir.y = dot(p, b);
                       vLightDir.z = dot(p, n);

                       vEyeDir = uNMatrix * aVertexPosition;

                       vEyeDir.x = dot(vEyeDir, t);
                       vEyeDir.y = dot(vEyeDir, b);
                       vEyeDir.z = dot(vEyeDir, n);

                   }
    </SCRIPT>




    <SCRIPT id="fbump" type="x-shader/x-fragment">
                   precision mediump float;

                   varying vec2 vTextureCoord;
                   varying vec3 vLightDir;
                   varying vec3 vEyeDir;

                   uniform vec3 uLightColor;
                   
                   uniform sampler2D uSampler;
                   uniform sampler2D uNormalSampler;

                   void main( void ) {
                       vec3 eyeDir = normalize(vEyeDir);
                       vec3 lightDir = normalize(vLightDir);

                       vec4 nCol = texture2D( uNormalSampler, vTextureCoord );
                       vec3 map = normalize(nCol.xyz * 2.0 - 1.0);

                       // diffuse
                       //float litContrib = max(dot(map, lightDir), 0.0); // "Standard Lambert"
                       float litContrib = dot(map, lightDir)*0.5 + 0.5; // "Half-Lambert"
                       vec4 litColor = vec4(litContrib * uLightColor, 1);

                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );

                       gl_FragColor = col*litColor;
                   }

                   void ex_main( void ) {
                       vec3 eyeDir = normalize(vEyeDir);
                       vec3 lightDir = normalize(vLightDir);

                       vec4 nCol = texture2D( uNormalSampler, vTextureCoord );
                       vec3 map = normalize(nCol.xyz * 2.0 - 1.0);

                       // diffuse
                       float litContrib = max(dot(map, lightDir), 0.0);
                       vec4 litColor = litContrib * vec4(uLightColor, 1);

                       //specular
                       vec3 reflectDir = reflect(lightDir, map);
                       float specContrib = max(dot(eyeDir, reflectDir), 0.0);
                       specContrib = pow(specContrib, 3.0); // hardcoded specular power!!!
                       vec4 specColor = specContrib * vec4(1,1,1,1); // hardcoded specular colour!!!

                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );

                       gl_FragColor = col*litColor + specColor;
                   }
    </SCRIPT>

    <SCRIPT id="ftoonbump" type="x-shader/x-fragment">
                   precision mediump float;

                   varying vec2 vTextureCoord;
                   varying vec3 vLightDir;
                   varying vec3 vEyeDir;

                   uniform vec3 uLightColor;
                   
                   uniform sampler2D uSampler;
                   uniform sampler2D uNormalSampler;

                   void main( void ) {
                       vec3 eyeDir = normalize(vEyeDir);
                       vec3 lightDir = normalize(vLightDir);

                       vec4 nCol = texture2D( uNormalSampler, vTextureCoord );
                       vec3 map = normalize(nCol.xyz * 2.0 - 1.0);

                       // diffuse
                       //float litContrib = max(dot(map, lightDir), 0.0); // "Standard Lambert"
                       float litContrib = dot(map, lightDir)*0.5 + 0.5; // "Half-Lambert"

                       // step shading
                       if ( litContrib < 0.2 ) litContrib = 0.2;
                       else if ( litContrib < 0.5 ) litContrib = smoothstep(0.2,0.22,litContrib)*0.3+0.2;
                       else if ( litContrib < 0.65 ) litContrib = smoothstep(0.5, 0.52, litContrib)*0.15+0.5;
                       else if ( litContrib < 0.9 ) litContrib = smoothstep(0.65, 0.67, litContrib)*0.25+0.65;
                       else litContrib = smoothstep(0.9,0.92,litContrib)*0.1+0.9;
                       //if ( litContrib < 0.2 ) litContrib = 0.2;
                       //else if ( litContrib < 0.5 ) litContrib = 0.5;
                       //else if ( litContrib < 0.65 ) litContrib = 0.65;
                       //else if ( litContrib < 0.9 ) litContrib = 0.9;
                       //else litContrib = 1.0;


                       vec4 litColor = vec4(litContrib * uLightColor, 1);


                       vec4 col = texture2D( uSampler, vec2( vTextureCoord.s, vTextureCoord.t ) );

                       gl_FragColor = col*litColor;
                   }
    </SCRIPT>
    
  </HEAD>

  <BODY onLoad="webGLStart()" style="width: 100%, height: 100%, body: 100%;">

    <CANVAS id="maincanvas" style="border: none; background-color: black;" width="1280" height="720"></CANVAS>
    <P id="output">Michael Labbe's plane demo</P>
    
  </BODY>

  
</HTML>
